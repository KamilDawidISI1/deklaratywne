% Wyznaczanie stopnia wielomianu
% o współczynnikach liczbowych

/*
Definicja strukturalna

1) st(x) = 1
   st(c) = 0, gdzie c - liczba

2) Stopnie wielomianów złożonych określamy następująco:
	st(-W) = st(W),
	st(W1+W2)=st(W1-W2)=max(st(W1),st(W2)),
	st(W1*W2)=st(W1)+st(W2),
	st(W^N)=st(W)*N (N - liczba naturalna, N>1) 

*/

% st_wielomian(W,X,N)
% spełniony, gdy N jest stopniem wielomianu W
% zmiennej X o współczynnikach liczbowych

% warunek zakończenia rekurencji
	st_wielomian(X,X,1).
	st_wielomian(C,_,0):-number(C).
% rekurencja
	st_wielomian(-W,X,N):-st_wielomian(W,X,N).
	st_wielomian(W1+W2,X,N):-st_wielomian(W1,X,N1),st_wielomian(W2,X,N2),N is max(N1,N2).
	st_wielomian(W1-W2,X,N):-st_wielomian(W1,X,N1),st_wielomian(W2,X,N2),N is max(N1,N2).
	st_wielomian(W1*W2,X,N):-st_wielomian(W1,X,N1),st_wielomian(W2,X,N2),N is N1+N2.
	st_wielomian(W^A,X,N):-integer(A),A>1,st_wielomian(W,X,N1),N is N1*A. 
	
/*
7 klauzul
1 fakt
6 reguł
1 definicja relacji
*/

/*
Sesja prolog

?- st_wielomian(y,y,1).
true ;
false.

?- st_wielomian(x,x,N).
N = 1 ;
false.

?- st_wielomian(x^2,x,N).
N = 2 ;
false.

?- st_wielomian(3*x*x,x,N).
N = 2 ;
false.

?- st_wielomian(5*x^2+x+1,x,N).
N = 2 ;
false.

?- st_wielomian(x^5+2*x+1,x,N).
N = 5 ;
false.



*/
